buildscript {
    ext.kotlin_version = '1.3.61'
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "net.onedaybeard.artemis:artemis-odb-gradle-plugin:2.2.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

apply from: 'dependencies.gradle'

dependencies {
    //Plugins compiles :server so root depends on both
    compile project(":content")
    compile project(":plugins")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

test {
    useJUnitPlatform()
}

allprojects {
    apply plugin: "kotlin"
    apply plugin: "artemis"

    group 'worlds.gregs.hestia'
    version '0.3.1'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile fileTree(dir: '../libs', includes: ['*.jar'])
        compile systemDependencies.values()
        compile utilityDependencies.values()
        compile mainDependencies.values()
        compile logDependencies.values()
        testCompile testDependencies.values()
    }

    weave {
        enableArtemisPlugin = true
        enablePooledWeaving = true
        generateLinkMutators = true
        optimizeEntitySystems = true
        dependsOn build
        classesDir = sourceSets.main.output.classesDirs.first()
    }
    
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}